import os.path

Import('*')


def filter_cc(list):
    return [x for x in list if os.path.splitext(str(x))[1] in [".cc"]]


common_files = Glob('*.cpp') + Glob('aux/*.cpp') + Glob('utils/*.cpp') + Glob('*.c')
diane_files =  Glob('diane/*.cpp') 
sophos_files = Glob('sophos/*.cpp')

protos = env.SConscript('protos/build.scons', exports='env')

common_objs = env.Object(common_files, CPPPATH = ['.'] + env.get('CPPPATH', []))
diane_objs = env.Object(diane_files + filter_cc(protos["diane"]), CPPPATH = ['.'] + env.get('CPPPATH', []))
sophos_objs = env.Object(sophos_files + filter_cc(protos["sophos"]), CPPPATH = ['.'] + env.get('CPPPATH', []))

objs = {}
objs["diane"] = diane_objs + common_objs
objs["sophos"] = sophos_objs + common_objs

Return('objs')